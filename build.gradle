plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id 'io.qameta.allure' version '2.11.2'
}

group = 'com.distribuida'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
	// IMPORTANTE: Oracle UCP NO está en Maven Central, para usarlo necesitas este repo y credenciales.
	// Si quieres usar UCP, descomenta y configura usuario y contraseña Oracle Maven Repository:
	/*
	maven {
		url = uri("https://maven.oracle.com")
		credentials {
			username = 'tu-usuario-oracle'
			password = 'tu-password-oracle'
		}
	}
	*/
}

configurations {

	compileOnly {
		extendsFrom annotationProcessor
		exclude group: 'org.aspectj'
	}
	allureAspectjWeaverAgent {
		canBeResolved = true
		canBeConsumed = false
	}
}

dependencies {
	// SPRING
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// DATA SOURCE POOL
	implementation 'com.zaxxer:HikariCP:5.0.1' // Usa versión fija y reciente


	// LOMBOK
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// DEV TOOLS
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// DRIVER MYSQL
	runtimeOnly 'com.mysql:mysql-connector-j:8.1.0'

	// TESTING
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.mockito:mockito-core:5.12.0'
	testImplementation 'org.mockito:mockito-junit-jupiter:5.12.0'
	testImplementation 'io.qameta.allure:allure-junit5:2.24.0'

	// ASPECTJ para Allure
	allureAspectjWeaverAgent 'org.aspectj:aspectjweaver:1.9.19'
}

task copyAllureAspectjWeaverAgent(type: Copy) {
	from configurations.allureAspectjWeaverAgent
	into "$buildDir/allure"
}

tasks.named('test') {
	dependsOn copyAllureAspectjWeaverAgent
	useJUnitPlatform()
	jvmArgs = [
			"-javaagent:$buildDir/allure/aspectjweaver-1.9.19.jar",
			"--add-opens=java.base/java.lang=ALL-UNNAMED"
	]
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = false
		csv.required = false
		html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
	}
}
